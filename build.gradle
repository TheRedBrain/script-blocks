plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT'
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {

	// Cloth Config
	maven {
		url "https://maven.shedaniel.me/"
	}

	// Cloth Config, Mod Menu
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}

	// Cardinal Components
	maven {
		name = "Ladysnake Libs"
		url = 'https://maven.ladysnake.org/releases'
	}

	// Immersive Portals
	maven {
		url 'https://jitpack.io'
	}

	// Slot Customization API
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}

}

loom {
	splitEnvironmentSourceSets()

	mods {
		"scriptblocks" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

sourceSets {
	testmod {
		compileClasspath += sourceSets.main.compileClasspath
		runtimeClasspath += sourceSets.main.runtimeClasspath
	}

	testmodClient {
		compileClasspath += sourceSets.main.compileClasspath
		runtimeClasspath += sourceSets.main.runtimeClasspath
		compileClasspath += sourceSets.client.compileClasspath
		runtimeClasspath += sourceSets.client.runtimeClasspath

		compileClasspath += sourceSets.testmod.compileClasspath
		runtimeClasspath += sourceSets.testmod.runtimeClasspath
	}
}

loom {
	runs {
		testmodClient {
			client()
			ideConfigGenerated project.rootProject == project
			name = "Testmod Client"
			source sourceSets.testmodClient
		}
		testmodServer {
			server()
			ideConfigGenerated project.rootProject == project
			name = "Testmod Server"
			source sourceSets.testmod
		}
	}

	mods {
		"scriptblocks_test" {
			sourceSet sourceSets.testmod
			sourceSet sourceSets.testmodClient
		}
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Cloth Config
	modImplementation ("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config}")

	// Immersive Portals Core
	modImplementation "com.github.iPortalTeam.ImmersivePortalsMod:imm_ptl_core:${project.q_misc_util_version}"

	// Miscellaneous Utility Library
	modImplementation "com.github.iPortalTeam.ImmersivePortalsMod:q_misc_util:${project.q_misc_util_version}"

	// Cardinal Components
	modImplementation ("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}")
	modImplementation ("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cca_version}")

	// Mod Menu
	modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"

	// Slot Customization API
	modImplementation "maven.modrinth:slot-customization-api:${project.slot_customization_api_version}"

	// Test mod
	testmodImplementation sourceSets.main.output
	testmodClientImplementation sourceSets.main.output
	testmodClientImplementation sourceSets.client.output
	testmodClientImplementation sourceSets.testmod.output
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

import com.modrinth.minotaur.dependencies.ModDependency
modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = 'ubmgmYs9' // The ID of your Modrinth project. Slugs will not work.
	versionNumber = mod_version
	versionType = "alpha" // can also be "beta" or "alpha"
	uploadFile = remapJar
	gameVersions = ['1.20.1'] // An array of game versions the version supports
	loaders = ['fabric']
	dependencies = [
			new ModDependency('P7dR8mSH', 'required'), // Fabric API
			new ModDependency('9s6osm5g', 'required'), // Cloth Config
			new ModDependency('zJpHMkdD', 'required'), // Immersive Portals
			new ModDependency('K01OU20C', 'required'), // Cardinal Components
			new ModDependency('QYZ7JTtD', 'required') // Slot Customization API
	]
	changelog = "${update_changelog}"
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}